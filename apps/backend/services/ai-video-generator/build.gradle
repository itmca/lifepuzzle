plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'java'
    id 'checkstyle'
}

jar {
    enabled = false
}

static def getDate() {
    new Date().format('yyyy.MM')
}

group = 'io.itmca'
version = getDate()

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
    maven {
        name = "TarsosDSP repository"
        url = "https://mvn.0110.be/releases"
    }
}

ext {
    sentryVersion = '8.12.0'
    jwtVersion = '0.12.6'
}

dependencies {
    // Shared modules
    implementation project(':shared:java-common')
    
    // Web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // AWS
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3:3.4.0'
    
    // Spring Cloud Stream RabbitMQ
    implementation 'org.springframework.cloud:spring-cloud-starter-stream-rabbit:4.1.3'

    // Data
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Monitoring
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Security (for basic auth if needed)
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // Tool
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    implementation 'org.json:json:20240303'
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
}


processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

task buildZip(type: Zip) {
    from('src/.platform') {
        into '.platform'
    }

    from(bootJar.outputs.files) {
        into "."
    }
    destinationDirectory = layout.buildDirectory.get().asFile
    archiveFileName = project.name + ".zip"
}

checkstyle {
    toolVersion('10.10.0')
    configFile = rootProject.file('tools/checkstyle/checkstyle.xml')
    maxWarnings = 0
}